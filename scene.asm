; // ==================================
; // scene.asm
; // ----------------------------------
; // Scenes are classes that hold a single section of a game. A scene contains a
; // pointer to a list of GameObjects that is updated by the scene_update function.
; // The scene also updates simulations and any game logic that isn't handled directly
; // by GameObjects or their components. At the end of every update, the scene will
; // call the renderer module to render its contents.
; // ==================================

INCLUDE default_header.inc
INCLUDE heap_functions.inc
INCLUDE scene.inc
INCLUDE game_object.inc
INCLUDE game_object_ids.inc
INCLUDE renderer.inc
INCLUDE render_command.inc
INCLUDE camera.inc

.code
; // ********************************************
; // Constructor Methods
; // ********************************************

; // ----------------------------------
; // init_scene
; // Initializes memory with the contents of a Scene
; // 
; // Register Parameters: 
; //	ecx - THIS pointer
; // ----------------------------------
init_scene PROC PUBLIC USES esi, maxGameObjects : DWORD
	mov esi, ecx ; // Save off my THIS pointer in esi

	lea ecx, (Scene PTR [esi]).camera
	INVOKE init_camera, 0, 0

	lea ecx, (Scene PTR [esi]).gameObjects
	INVOKE init_unordered_vector, maxGameObjects

	lea ecx, (Scene PTR [esi]).startQueue
	INVOKE init_unordered_vector, maxGameObjects

	lea ecx, (Scene PTR [esi]).freeQueue
	INVOKE init_unordered_vector, maxGameObjects
	
	lea ecx, (Scene PTR [esi]).renderCommands
	INVOKE init_unordered_vector, maxGameObjects

	mov ecx, esi ; // Restore my THIS pointer
		
	ret
init_scene ENDP

; // ----------------------------------
; // new_scene
; // Reserves heap space for the scene with parameters and calls the initializer method
; // ----------------------------------
new_scene PROC PUBLIC USES ecx, maxGameObjects : DWORD
	INVOKE HeapAlloc, hHeap, HEAP_GENERATE_EXCEPTIONS, SIZEOF Scene
	mov ecx, eax ; // Move the memory address to ecx so it can function as a "this" pointer
	INVOKE init_scene, maxGameObjects
	mov eax, ecx ; // Now return the THIS pointer

	ret ; // Return with the address of the memory block in HeapAlloc
new_scene ENDP

; // ----------------------------------
; // free_scene
; // Convenient method for freeing a Scene
; //
; // Register Parameters: 
; //	ecx - THIS pointer
; // ----------------------------------
free_scene PROC PUBLIC
	INVOKE HeapFree, hHeap, 0, ecx
	ret
free_scene ENDP


; // ********************************************
; // Class methods
; // ********************************************

; // ----------------------------------
; // add_game_object
; // Adds a game object to the scene's start queue
; //
; // Register Parameters: 
; //	ecx - THIS pointer
; // ----------------------------------
instantiate_game_object PROC PUBLIC USES esi, pGameObject: DWORD
	mov esi, ecx ; // Store the THIS pointer in ecx

	lea ecx, (Scene PTR[ecx]).startQueue
	INVOKE push_back, pGameObject

	mov ecx, esi ; // Restore the THIS pointer
	ret
instantiate_game_object ENDP

; // ----------------------------------
; // scene_process_start_queue
; // Moves the GameObjects from the startQueue to the gameObjects vector 
; // and then calls their start() methods.
; //
; // Register Parameters: 
; //	ecx - THIS pointer
; // ----------------------------------
scene_process_start_queue PROC PRIVATE USES eax ebx ecx edx esi edi
	local pThis
	mov pThis, ecx ; // Save the THIS pointer just in case

	lea ecx, (Scene PTR [ecx]).startQueue
	mov ebx, (UnorderedVector PTR [ecx]).count
	mov eax, (UnorderedVector PTR [ecx]).pData
	mov edx, 0 ; // int i = 0
	
	; // Iterate through the Game Objects and add them to the gameObjects vector, then call their start methods
	.WHILE edx < ebx
		; // esi = startQueue[i]
		mov esi, [eax + edx * 4]
		
		; // push the game object startQueue[i] into GameObjects
		mov ecx, pThis
		lea ecx, (Scene PTR [ECX]).gameObjects
		INVOKE push_back, esi
	
		; // call the start() method in the GameObject startQueue[i]
		mov ecx, esi
		INVOKE game_object_start_virtual
		inc edx
	.ENDW

	; // Set the length of startQueue to be 0, effectively clearing it
	mov ecx, pThis
	lea ecx, (Scene PTR [ecx]).startQueue
	mov (UnorderedVector PTR [ecx]).count, 0

	mov ecx, pThis
	ret
scene_process_start_queue ENDP

; // ----------------------------------
; // scene_update_game_objects
; // Calls the update method of all of the GameObjects in gameObjects
; //
; // Register Parameters: 
; //	ecx - THIS pointer
; // ----------------------------------
scene_update_game_objects PROC PRIVATE USES eax ebx ecx edx esi edi, deltaTime: REAL4
	local pThis
	mov pThis, ecx ; // Save the THIS pointer just in case

	lea ecx, (Scene PTR [ecx]).gameObjects
	mov ebx, (UnorderedVector PTR [ecx]).count
	mov eax, (UnorderedVector PTR [ecx]).pData
	mov edx, 0 ; // int i = 0
	
	; // Iterate through the Game Objects and call their update methods if they are still alive
	.WHILE edx < ebx
		; // esi = gameObjects[i]
		mov esi, [eax + edx * 4]

		; // call the update() method in the GameObject gameObjects[i]
		push eax

		mov ecx, esi
		INVOKE game_object_update_virtual, deltaTime

		pop eax
		inc edx
	.ENDW

	mov ecx, pThis
	ret
scene_update_game_objects ENDP

; // ----------------------------------
; // scene_free_game_objects
; // Frees the game objects in the freeQueue and removes them from the gameObjects vector
; //
; // Register Parameters: 
; //	ecx - THIS pointer
; // ----------------------------------
scene_free_game_objects PROC PRIVATE USES eax ebx ecx edx esi edi
	local pThis
	mov pThis, ecx ; // Save the THIS pointer just in case

	lea ecx, (Scene PTR [ecx]).freeQueue
	mov ebx, (UnorderedVector PTR [ecx]).count
	mov eax, (UnorderedVector PTR [ecx]).pData
	mov edx, 0 ; // int i = 0
	
	; // Iterate through the freeQueue vector
	; // Remove the element from gameObjects and free it
	.WHILE edx < ebx
		; // esi = freeQueue[i]
		mov esi, [eax + edx * 4]

		; // Remove the element pointer from gameObjects
		mov ecx, pThis
		lea ecx, (Scene PTR [ecx]).gameObjects
		INVOKE remove_element, esi

		; // call the free() method on the GameObject
		mov ecx, esi
		INVOKE free_game_object
		inc edx
	.ENDW

	; // Set the length of freeQueue to be 0, effectively clearing it
	mov ecx, pThis
	lea ecx, (Scene PTR [ecx]).freeQueue
	mov (UnorderedVector PTR [ecx]).count, 0

	mov ecx, pThis
	ret
scene_free_game_objects ENDP

; // ----------------------------------
; // scene_render_frame
; // Build the RenderCommand list and pass it to the renderer
; //
; // Register Parameters: 
; //	ecx - THIS pointer
; // ----------------------------------
scene_render_frame PROC PRIVATE USES eax ebx edx esi edi
	local pThis
	mov pThis, ecx

	; // First clear the render list
	lea ecx, (Scene PTR [ecx]).renderCommands
	mov (UnorderedVector PTR [ecx]).count, 0

	; // Build render list
	mov ecx, pThis
	lea ecx, (Scene PTR [ecx]).gameObjects
	mov ebx, (UnorderedVector PTR [ecx]).count
	mov eax, (UnorderedVector PTR [ecx]).pData
	mov edx, 0 ; // int i = 0
	
	; // Iterate through the Game Objects
	.WHILE edx < ebx
		; // esi = gameObjects[i]
		mov esi, [eax + edx * 4]

		; // If the object is pending free, skip it
		mov ecx, (GameObject PTR [esi]).awaitingFree
		.IF ecx != 0
			.CONTINUE
		.ENDIF

		push eax
		; // If it has a SpriteComponent or a RectComponent, add the render command
		; // ----------------------------------------------------------------------
		; // First check if there's a RectComponent
		mov ecx, esi
		INVOKE get_first_component_which_is_a, RECT_COMPONENT_ID
		
		.IF eax != 0
			; // Create the new RenderCommand
			push ebx
			mov ebx, eax

			mov ecx, esi
			INVOKE get_first_component_which_is_a, TRANSFORM_COMPONENT_ID ; // Get the transform pointer from the GameObject
			INVOKE new_render_command, RC_RECT, eax, ebx

			pop ebx

			; // Add the render command to the RenderCommands list
			mov ecx, pThis
			lea ecx, (Scene PTR [ecx]).renderCommands
			INVOKE push_back, eax 

			jmp scene_render_frame_loop_exit
		.ENDIF

		; // Now check if there's a SpriteComponent
		mov ecx, esi
		INVOKE get_first_component_which_is_a, SPRITE_COMPONENT_ID
		
		.IF eax != 0
			; // Create the new RenderCommand
			push ebx
			mov ecx, esi

			mov ebx, eax
			INVOKE get_first_component_which_is_a, TRANSFORM_COMPONENT_ID ; // Get the transform pointer from the GameObject
			INVOKE new_render_command, RC_SPRITE, eax, eax

			pop ebx

			; // Add the render command to the RenderCommands list
			mov ecx, pThis
			lea ecx, (Scene PTR [ecx]).renderCommands
			INVOKE push_back, eax 

			jmp scene_render_frame_loop_exit
		.ENDIF

		scene_render_frame_loop_exit:
		; // Restore the pointer to pData and continue
		pop eax
		inc edx ; // i++
	.ENDW

	mov ecx, pThis
	lea esi, (Scene PTR [ecx]).camera
	lea ecx, (Scene PTR [ecx]).renderCommands
	mov ebx, (UnorderedVector PTR [ecx]).count
	mov eax, (UnorderedVector PTR [ecx]).pData
	mov edx, 0 ; // int i = 0

	; // Pass render list to renderer
	INVOKE renderCommands, eax, ebx, esi
	ret
scene_render_frame ENDP

; // ----------------------------------
; // scene_update
; // Responsible for updating the game objects and simulations every frame.
; //
; // Register Parameters: 
; //	ecx - THIS pointer
; // ----------------------------------
scene_update PROC PUBLIC USES eax ebx ecx edx esi edi, deltaTime: REAL4
	local pThis
	mov pThis, ecx ; // Save the THIS pointer just in case

	; // Take Input and set input flags

	; // Process start queue
	INVOKE scene_process_start_queue

	; // Update time sensitive components (such as timers and tweens) NYI
	; // for (GameObject o : *pGameObjects):
	; //	for (Component c : o->pComponents)
	; //		if (c.componentType == TIMER_COMPONENT_TYPE)
	; //			c.Update(deltaTime)

	; // Update all of the GameObject logic
	INVOKE scene_update_game_objects, deltaTime

	; // Update animator components NYI
	; // for (GameObject o : *pGameObjects):
	; //	for (Component c : o->pComponents)
	; //		if (c.componentType == ANIMATOR_COMPONENT_TYPE)
	; //			c.Update(deltaTime)

	; // Free any GameObjects that were queued to be freed by gameplay logic
	INVOKE scene_free_game_objects

	; // Render the scene
	INVOKE scene_render_frame
	
	ret
scene_update ENDP

END
